DUL_find_best_match:
  type: procedure
  debug: false
  DUL:
    author:
    - BlackCoyote
    description:
    - Returns the best search match out of a list.
    usage:
    - <proc[DUL_find_best_match].context[<list separated by '/'>|<value>]>
    example:
    - <proc[DUL_find_best_match].context[<server.flag[homes].as_list.separated_by[/]>|<context.args.get[1]>]>
  script:
  - define list <def[1].split_by[/]>
  - if <def[list].is_empty||true> {
    - debug error "DUL - No valid list specified!"
    - determine null
    }
  - if <def[2]||null> == null {
    - debug error "DUL - No valid value specified!"
    - determine null
    }
  - if <def[list].contains[<def[2]>]> {
    - determine <def[2]>
    } else if <def[list].filter[starts_with[<def[2]>]].size||0> > 0 {
    - determine <def[list].filter[starts_with[<def[2]>]].sort_by_number[length].first||null>
    } else if <def[list].filter[contains[<def[2]>]].size||0> > 0 {
    - determine <def[list].filter[contains[<def[2]>]].sort_by_number[length].first||null>
    } else {
    - determine null
    }